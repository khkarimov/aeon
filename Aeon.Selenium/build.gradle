def baseProjectName = 'Aeon.Selenium'
def baseProjectDescription = 'Aeon Selenium'
def base = baseProjectName

group 'aeon'
version '0.2.11'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    aeon
    compile.extendsFrom(aeon)
}

ext {
    seleniumVersion = '3.5.3'
    appiumVersion = "5.0.2"
}

dependencies {
    compile project(':Aeon.Core')

    // Drivers
    compile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    compile "org.seleniumhq.selenium:selenium-ie-driver:$seleniumVersion"
    compile "org.seleniumhq.selenium:selenium-edge-driver:$seleniumVersion"
    compile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    compile "org.seleniumhq.selenium:selenium-support:$seleniumVersion"

    compile "io.appium:java-client:$appiumVersion"
}

jar {
    version = version
    baseName = base
    archiveName = base + '-' + version + '.jar'

    manifest {
        attributes 'Implementation-Title': baseProjectDescription,
                'Implementation-Version': version,
                'Plugin-Id': 'SeleniumAeon',
                'Plugin-Version': version,
                'Plugin-Class': 'aeon.selenium.SeleniumPlugin'
    }
}

task fatJar(type: Jar) {
    group 'build'
    version = version
    baseName = base
    archiveName = base + '-all-' + version + '.jar'

    manifest {
        attributes 'Implementation-Title': baseProjectDescription,
                'Implementation-Version': version,
                'Plugin-Id': 'SeleniumAeon',
                'Plugin-Version': version,
                'Plugin-Class': 'aeon.selenium.SeleniumPlugin'
    }

    from configurations.compile.collect {
        it.isDirectory() ? it : zipTree(it)
    }
    with jar
}

task buildPlugin(type: Jar) {
    group 'build'
    baseName = base
    version = version
    into('classes') {
        with jar
    }
    extension('zip')
}

jar.finalizedBy(buildPlugin)

apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(file("$buildDir/libs/Aeon.Selenium-" + version + ".zip"))
            artifactId 'aeon.selenium'
        }
    }
}
