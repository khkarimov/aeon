apply plugin: 'java'
group 'Aeon'
compileTestJava.options.encoding = 'utf-8'

repositories {
    mavenCentral()
}

dependencies {
    compile project('Aeon.Appium')
    compile project('Aeon.Selenium')
    compile project('Aeon.Core')
    compile project('Aeon.SystemTests')
}

//Copy selenium plugin into SystemTests plugins folder
task CopySeleniumPlugin(type: Copy) {
    group 'copy'
    from 'Aeon.Selenium/build/libs'
    into 'Aeon.SystemTests/plugins'
    include '*.jar'
}

assemble.dependsOn(clean)
assemble.dependsOn(javadoc)
assemble.finalizedBy(CopySeleniumPlugin)

allprojects {
    apply plugin: 'checkstyle'
    checkstyle {
        configFile = new File(rootDir, "checkstyle.xml")
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.3"
    }
}

apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'

subprojects {
    artifactory {
        contextUrl = 'http://artifactory.mia.ucloud.int/artifactory/'
        publish {
            repository {
                repoKey = 'ultimate-gradle-local' // The Artifactory repository key to publish to
                username = "${artifactory_username}" // The publisher user name
                password = "${artifactory_password}" // The publisher password
            }
            defaults {
                // Reference to Gradle publications defined in the build script.
                // This is how we tell the Artifactory Plugin which artifacts should be
                // published to Artifactory.
                publications('mavenJava')
                publishArtifacts = true
                // Properties to be attached to the published artifacts.
                properties = ['qa.level': 'basic', 'dev.team': 'core']
                // Publish generated POM files to Artifactory (true by default)
                publishPom = true
            }
        }
        resolve {
            repoKey = 'jcenter'
        }
    }
}

task wrapper(type: Wrapper) {
    group 'build setup'
    gradleVersion = '4.0'
}

artifactoryPublish.skip = true
